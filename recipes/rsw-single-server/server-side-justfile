set dotenv-load

SERVER_IP_ADDRESS := env_var("SERVER_IP_ADDRESS")
SERVER_PUBLIC_DNS := env_var("SERVER_PUBLIC_DNS")
RSW_LICENSE := env_var("RSW_LICENSE")
RSW_URL := env_var("RSW_URL")
RSW_FILENAME := env_var("RSW_FILENAME")

R_VERSION := env_var_or_default("R_VERSION", "4.1.2")
PYTHON_VERSION := env_var_or_default("PYTHON_VERSION", "3.10.4")

# -----------------------------------------------------------------------------
# Build RSW
# -----------------------------------------------------------------------------

# Install RStudio workbench and all of the dependencies
build-rsw: 
    # Basic setup
    sudo apt-get update
    sudo apt-get update
    sudo apt-get install -y gdebi-core

    # Add some test users
    just add-user sam password
    just add-user jake password
    just add-user olivia password

    # Install RSW and required dependencies
    just install-r 
    just symlink-r
    just install-python
    just install-rsw
    just install-vscode
    sudo cp -r /etc/rstudio /etc/rstudio-original-conf-files

    # Set up SSL
    just ssl-self-sign-cert
    
    # Set up config files
    just set-rserver-conf
    just set-launcher-conf
    just set-vscode-extensions-conf

    # Restart
    sudo rstudio-server restart

    # For some reason sudo rstudio-launcher restart is throwing an error.  
    # Use the || operator so that justfile does not think the recipe failed.
    sudo rstudio-launcher restart || echo

# -----------------------------------------------------------------------------
# Install
# -----------------------------------------------------------------------------

install-rsw:
    curl -O {{RSW_URL}}
    sudo gdebi -n {{RSW_FILENAME}}
    sudo rstudio-server license-manager activate $RSW_LICENSE

install-r:
    curl -O https://cdn.rstudio.com/r/ubuntu-2004/pkgs/r-{{R_VERSION}}_1_amd64.deb
    sudo gdebi -n r-{{R_VERSION}}_1_amd64.deb

install-python:
    # https://docs.rstudio.com/resources/install-python/
    # install python
    sudo mkdir /opt/python
    sudo curl -fsSL -o /opt/python/miniconda.sh https://repo.anaconda.com/miniconda/Miniconda3-latest-Linux-x86_64.sh 
    sudo chmod 755 /opt/python/miniconda.sh
    sudo /opt/python/miniconda.sh -b -p /opt/python/miniconda
    sudo /opt/python/miniconda/bin/conda create --quiet --yes --prefix /opt/python/{{PYTHON_VERSION}} --channel conda-forge python={{PYTHON_VERSION}}
    sudo /opt/python/{{PYTHON_VERSION}}/bin/pip install --upgrade pip setuptools wheel
    # make jupyter kernel
    sudo /opt/python/{{PYTHON_VERSION}}/bin/pip install ipykernel
    sudo /opt/python/{{PYTHON_VERSION}}/bin/python -m ipykernel install --name py{{PYTHON_VERSION}} --display-name "Python {{PYTHON_VERSION}}"

install-vscode:
    sudo rstudio-server install-vs-code /opt/code-server

# -----------------------------------------------------------------------------
# Linux mgmt
# -----------------------------------------------------------------------------

add-user name password:
    #!/bin/bash
    sudo useradd --create-home --home-dir /home/{{name}} -s /bin/bash {{name}};
    echo -e '{{password}}\n{{password}}' | sudo passwd {{name}};

symlink-r:
    sudo ln -s /opt/R/{{R_VERSION}}/bin/R /usr/local/bin/R
    sudo ln -s /opt/R/{{R_VERSION}}/bin/Rscript /usr/local/bin/Rscript

ssl-self-sign-cert:
    just set-ssl-san-cnf
    sudo openssl req -x509 -nodes -days 365 -newkey rsa:2048 -keyout /etc/ssl/server.key -out /etc/ssl/server.crt -config /etc/ssl/san.cnf

# -----------------------------------------------------------------------------
# Configuration files
# -----------------------------------------------------------------------------

set-rserver-conf:
    #!/bin/bash
    sudo bash -c 'cat <<EOF > /etc/rstudio/rserver.conf
    www-port=443
    admin-enabled=1
    admin-group=sam
    server-health-check-enabled=1

    # SSL
    ssl-enabled=1
    ssl-certificate=/etc/ssl/server.crt
    ssl-certificate-key=/etc/ssl/server.key

    # Launcher Config
    launcher-address=127.0.0.1
    launcher-port=5559
    launcher-sessions-enabled=1
    launcher-default-cluster=Local
    launcher-sessions-callback-address=http://127.0.0.1:8787
    EOF'

set-launcher-conf:
    #!/bin/bash
    sudo bash -c 'cat <<EOF > /etc/rstudio/launcher.conf    
    [server]
    address=127.0.0.1
    port=5559
    server-user=rstudio-server
    authorization-enabled=1
    thread-pool-size=4
    enable-debug-logging=1
    admin-group=rstudio-server

    [cluster]
    name=Local
    type=Local
    exe=/usr/lib/rstudio-server/bin/rstudio-local-launcher
    EOF'

set-vscode-extensions-conf:
    #!/bin/bash
    sudo bash -c 'cat <<EOF > /etc/rstudio/vscode.extensions.conf  
    quarto.quarto
    Ikuyadeu.r
    ms-python.python
    EOF'

# -----------------------------------------------------------------------------
# SSL
# -----------------------------------------------------------------------------

set-ssl-san-cnf:
    #!/bin/bash
    sudo bash -c 'cat <<EOF > /etc/ssl/san.cnf
    [req]
    default_bits  = 2048
    distinguished_name = req_distinguished_name
    req_extensions = req_ext
    x509_extensions = v3_req
    prompt = no
    [req_distinguished_name]
    countryName = XX
    stateOrProvinceName = N/A
    localityName = N/A
    organizationName = Self-signed certificate
    commonName = {{SERVER_PUBLIC_DNS}}
    [req_ext]
    subjectAltName = @alt_names
    [v3_req]
    subjectAltName = @alt_names
    [alt_names]
    IP.1 = {{SERVER_IP_ADDRESS}}
    EOF'

# -----------------------------------------------------------------------------
# Helpers
# -----------------------------------------------------------------------------

restart:
    sudo rstudio-server restart
    sudo rstudio-launcher restart

restart-rstudio-server:
    sudo rstudio-server restart

restart-launcher:
    sudo rstudio-launcher restart

status:
    sudo rstudio-server status

logs:
    sudo tail /var/log/rstudio/rstudio-server/rserver.log

logs-watch:
    sudo tail -f /var/log/rstudio/rstudio-server/rserver.log

edit:
    sudo vim /etc/rstudio/rserver.conf

edit-database:
    sudo vim /etc/rstudio/database.conf

edit-load-balancer:
    sudo vim /etc/rstudio/load-balancer

