set dotenv-load

SERVER_IP_ADDRESS := env_var("SERVER_IP_ADDRESS")
EFS_ID := env_var("EFS_ID")  # For example: 'fs-0ae474bb0403fc7c6'
RSW_LICENSE := env_var("RSW_LICENSE")
KUBERNETES_API_ENDPOINT := env_var("KUBERNETES_API_ENDPOINT")
KUBERNETES_CLUSTER_TOKEN := env_var("KUBERNETES_CLUSTER_TOKEN")
KUBERNETES_CERTIFICATE_AUTHORITY := env_var("KUBERNETES_CERTIFICATE_AUTHORITY")
NFS_IP_ADDRESS := env_var("NFS_IP_ADDRESS")

# -----------------------------------------------------------------------------
# Build RSW
# -----------------------------------------------------------------------------

# Install RStudio workbench and all of the dependencies
build-rsw: 
    # Basic setup
    just install-linux-tools 

    # Set up shared drive
    just install-efs-utils
    just mount-efs
    sudo mkdir -p /mnt/efs/rstudio-server/shared-storage

    # Add some test users
    just add-user sam password
    just add-user jake password
    just add-user olivia password

    # Install RSW and required dependencies
    just install-r 
    just symlink-r
    just install-rsw
    just generate-cookie-key

    # Set up config files
    just set-rserver-conf
    just set-launcher-conf
    just set-launcher-kubernetes-profiles-conf
    just set-launcher-kubernetes-conf
    just set-launcher-mounts

    # Restart
    sudo rstudio-server restart
    sudo rstudio-launcher restart

# -----------------------------------------------------------------------------
# Install
# -----------------------------------------------------------------------------

install-linux-tools:
    sudo apt-get update
    sudo apt-get install -y tree
    sudo apt-get install -y bat
    sudo apt-get install -y gdebi-core
    sudo apt-get install -y uuid

install-rsw:
    curl -O https://download2.rstudio.org/server/bionic/amd64/rstudio-workbench-2022.02.0-443.pro2-amd64.deb
    sudo gdebi -n rstudio-workbench-2022.02.0-443.pro2-amd64.deb 
    sudo rstudio-server license-manager activate $RSW_LICENSE

install-r r_version='4.1.2':
    curl -O https://cdn.rstudio.com/r/ubuntu-2004/pkgs/r-{{r_version}}_1_amd64.deb
    sudo gdebi -n r-{{r_version}}_1_amd64.deb

install-efs-utils:
    #!/bin/bash
    set -euxo pipefail
    sudo apt-get -y install binutils
    git clone https://github.com/aws/efs-utils
    cd efs-utils
    ./build-deb.sh
    sudo apt-get -y install ./build/amazon-efs-utils*deb

# -----------------------------------------------------------------------------
# Linux mgmt
# -----------------------------------------------------------------------------

add-user name password:
    #!/bin/bash
    sudo mkdir -p /mnt/efs/home
    sudo useradd --create-home --home-dir /mnt/efs/home/{{name}} -s /bin/bash {{name}};
    echo -e '{{password}}\n{{password}}' | sudo passwd {{name}};

mount-efs:
    sudo mkdir -p /mnt/efs;
    sudo mount -t efs -o tls {{EFS_ID}}:/ /mnt/efs;
    just set-fstab

generate-cookie-key:
    sudo sh -c "echo `uuid` > /etc/rstudio/secure-cookie-key"
    sudo chmod 0600 /etc/rstudio/secure-cookie-key

symlink-r r_version='4.1.2':
    sudo ln -s /opt/R/{{r_version}}/bin/R /usr/local/bin/R
    sudo ln -s /opt/R/{{r_version}}/bin/Rscript /usr/local/bin/Rscript

# -----------------------------------------------------------------------------
# Configuration files
# -----------------------------------------------------------------------------

set-rserver-conf:
    #!/bin/bash
    sudo bash -c 'cat <<EOF > /etc/rstudio/rserver.conf
    www-port=8787
    admin-enabled=1
    admin-group=sam
    server-health-check-enabled=1

    # Launcher Config
    # https://docs.rstudio.com/rsw/integration/launcher-kubernetes/
    launcher-address=127.0.0.1
    launcher-port=5559
    launcher-sessions-enabled=1
    launcher-default-cluster=Kubernetes
    launcher-sessions-callback-address=http://{{SERVER_IP_ADDRESS}}:8787
    launcher-sessions-container-run-as-root=0
    launcher-sessions-create-container-user=1

    # Share storage
    server-shared-storage-path=/mnt/efs/rstudio-server/shared-storage
    EOF'

set-launcher-conf:
    #!/bin/bash
    sudo bash -c 'cat <<EOF > /etc/rstudio/launcher.conf    
    [server]
    address=127.0.0.1
    port=5559
    server-user=rstudio-server
    admin-group=rstudio-server
    authorization-enabled=1
    thread-pool-size=4
    enable-debug-logging=1

    [cluster]
    name=Kubernetes
    type=Kubernetes
    EOF'

set-launcher-kubernetes-profiles-conf:
    #!/bin/bash
    sudo bash -c 'cat <<EOF > /etc/rstudio/launcher.kubernetes.profiles.conf
    [*]
    default-cpus=1
    default-mem-mb=512
    max-cpus=2
    max-mem-mb=1024
    container-images=rstudio/r-session-complete:centos7-2022.02.2-485.pro2
    default-container-image=rstudio/r-session-complete:centos7-2022.02.2-485.pro2
    allow-unknown-images=0
    EOF'

set-launcher-kubernetes-conf:
    #!/bin/bash
    sudo bash -c 'cat <<EOF > /etc/rstudio/launcher.kubernetes.conf
    api-url={{KUBERNETES_API_ENDPOINT}}
    auth-token={{KUBERNETES_CLUSTER_TOKEN}}
    certificate-authority={{KUBERNETES_CERTIFICATE_AUTHORITY}}
    kubernetes-namespace=rstudio-samedwardes
    EOF'

set-launcher-mounts:
    #!/bin/bash
    sudo bash -c 'cat <<EOF > /etc/rstudio/launcher-mounts
    Host: {{NFS_IP_ADDRESS}}
    Path: /home/{USER}
    MountPath: /mnt/efs/home/{USER}
    ReadOnly: false
    Cluster: Kubernetes
    EOF'

set-fstab:
    #!/bin/bash
    sudo bash -c 'cat <<EOF > /etc/fstab
    # mount efs
    {{EFS_ID}}:/ /etc/fstab efs defaults,_netdev 0 0
    EOF'

# -----------------------------------------------------------------------------
# Helpers
# -----------------------------------------------------------------------------

restart:
    sudo rstudio-server restart

restart-launcher:
    sudo rstudio-launcher restart

status:
    sudo rstudio-server status

logs:
    sudo tail /var/log/rstudio/rstudio-server/rserver.log

edit:
    sudo vim /etc/rstudio/rserver.conf

edit-database:
    sudo vim /etc/rstudio/database.conf

edit-load-balancer:
    sudo vim /etc/rstudio/load-balancer
